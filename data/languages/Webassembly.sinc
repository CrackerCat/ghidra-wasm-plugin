# sleigh include file for Skeleton language instructions

define token opcode (8)
	opc = (0,7)
;

define token byteval (8)
	bytev = (0,7)
;

################################################################
# Macros
################################################################

# These pcodeop's have NOP semantics, they just signal to the analyzer to modify the controlStack and valueStack
define pcodeop pushCallOther;
define pcodeop popCallOther;
define pcodeop beginLoopCallOther;
define pcodeop beginBlockCallOther;
define pcodeop endCallOther;
define pcodeop returnCallOther;

define pcodeop ifCallOther;
define pcodeop elseCallOther;

# These pcodeop's do not have NOP semantics, they are dynamically injected into by PcodeInjectLibraryWasm
define pcodeop brCallOther;

macro push32(val32) {
	pushCallOther(32:4);
	val64:8 = zext(val32);
	SP = SP - 8;
	*:8 SP = val64; 
}

macro pop32(ret32) {
	popCallOther(32:4);
	ret32 = *:4 SP;
	SP = SP + 8; 
}

macro push64(val64) {
	pushCallOther(64:4);
	SP = SP - 8;
	*:8 SP = val64; 
}

macro pop64(ret64) {
	popCallOther(64:4);
	ret64 = *:8 SP;
	SP = SP + 8; 
}

macro load8(memoffset, dest) {
	x0:4 = 0;
	pop32(x0);
	addr:4 = memoffset[0,31] + x0;
	dest = *[mem0]:1 addr;
}

macro load16(memoffset, dest) {
	x0:4 = 0;
	pop32(x0);
	addr:4 = memoffset[0,31] + x0;
	dest = *[mem0]:2 addr;
}

macro load32(memoffset, dest) {
	x0:4 = 0;
	pop32(x0);
	addr:4 = memoffset[0,32] + x0;
	dest = *[mem0]:4 addr;
}

macro load64(memoffset, dest) {
	x0:4 = 0;
	pop32(x0);
	addr:4 = memoffset[0,31] + x0;
	dest = *[mem0]:8 addr;
}

macro store8(memoffset, val) {
	x0:4 = 0;
	pop32(x0);
	addr:4 = memoffset[0,31] + x0;
	*[mem0]:1 addr = val;
}

macro store16(memoffset, val) {
	x0:4 = 0;
	pop32(x0);
	addr:4 = memoffset[0,31] + x0;
	*[mem0]:2 addr = val;
}

macro store32(memoffset, val) {
	x0:4 = 0;
	pop32(x0);
	addr:4 = memoffset[0,31] + x0;
	*[mem0]:4 addr = val;
}

macro store64(memoffset, val) {
	x0:4 = 0;
	pop32(x0);
	addr:4 = memoffset[0,31] + x0;
	*[mem0]:8 addr = val;
}


################################################################

define pcodeop trap;
define pcodeop get_global;
define pcodeop set_global;
define pcodeop popcnt;
define pcodeop clz;
define pcodeop ctz;
define pcodeop br_if;
define pcodeop rotl;
define pcodeop rotr;

@include "Leb128.sinc"

localidx: ULeb128 is ULeb128 {
	export ULeb128;
}

globalidx: ULeb128 is ULeb128 {
	export ULeb128;
}

typeidx: ULeb128 is ULeb128 {
	export ULeb128;
}

tableidx: ULeb128 is ULeb128 {
	export ULeb128;
}

reftype: "reftype="^bytev is bytev {
	val:1 = bytev;
	export val;
}

memalign: "align="^ULeb128 is ULeb128 {
	align:8 = ULeb128;
	export align;
}

memoffset: "offset="^ULeb128 is ULeb128 {
	off:8 = ULeb128;
	export off;
}

################################################################

indent: "" is indent_lvl=0 {}
indent: " . "^indent is indent [ indent_lvl = indent_lvl - 1; ]{}

# This outputs one less indentation than indent, use this when the indent_lvl is increased by the current instruction
inc_indent: "" is indent_lvl=1 | indent_lvl=0 {}
inc_indent: " . "^inc_indent is inc_indent [ indent_lvl = indent_lvl-1; ]{}

@define INC_INDENT "inc_indent [ indent_lvl = indent_lvl + 1; globalset(inst_next, indent_lvl); ]"

:""^indent^"unreachable" is opc=0x00; indent {
	trap();
}

:""^indent^"nop" is opc=0x01; indent {}

:""^inc_indent^"block" "blocktype="^ULeb128 is opc=0x02; ULeb128; $(INC_INDENT) {
	beginBlockCallOther();
}
:""^inc_indent^"loop" "blocktype="^ULeb128 is opc=0x03; ULeb128; $(INC_INDENT) {
	beginLoopCallOther();
}
:""^inc_indent^"if" "blocktype="^ULeb128 is opc=0x04; ULeb128; $(INC_INDENT) {
	val:4 = 0;
	pop32(val);
	if(val != 0) goto inst_next;
	ifCallOther();
}
:""^inc_indent^"else" is opc=0x05; inc_indent {
	elseCallOther();
}

:""^indent^"end" is opc=0x0b; indent_lvl=0; indent {
	returnCallOther();
}
:""^indent^"end" is opc=0x0b; indent [ indent_lvl = indent_lvl - 1; globalset(inst_next, indent_lvl); ] {
	endCallOther();
}

:""^indent^"br" ULeb128 is opc=0x0c; ULeb128; indent {
	brCallOther();
}
:""^indent^"br_if" ULeb128 is opc=0x0d; ULeb128; indent {
	val:4 = 0;
	pop32(val);
	if(val == 0) goto inst_next;
	brCallOther();
}
#TODO: br_table
#:""^indent^"br_table" is opc=0x0e; ULeb128; indent [br_table_count=ULeb128;] unimpl
:""^indent^"return" is opc=0x0f; indent {
	returnCallOther();
}
:""^indent^"call" ULeb128 is opc=0x10; ULeb128; indent unimpl
:""^indent^"call_indirect" typeidx tableidx is opc=0x11; typeidx; tableidx; indent unimpl

:""^indent^"ref.null" reftype is opc=0xd0; reftype; indent unimpl
:""^indent^"ref.is_null" is opc=0xd1; indent unimpl
:""^indent^"ref.func" ULeb128 is opc=0xd2; ULeb128; indent unimpl

:""^indent^"drop" is opc=0x1a; indent {
	val:8 = 0;
	pop64(val);
}
:""^indent^"select" is opc=0x1b; indent {
	cond:4 = 0;
	pop32(cond);
	val1:8 = 0;
	val2:8 = 0;
	pop64(val2);
	pop64(val1);
	if (cond != 0) goto <true>;
	push64(val1);
	goto inst_next;
<true>
	push64(val2);
}

#TODO: select t* 0x1c

:""^indent^"local.get" localidx is opc=0x20; localidx; indent {
	val:8 = *[register] (localidx:2 * 8);
	push64(val);
}
:""^indent^"local.set" localidx is opc=0x21; localidx; indent {
	val:8 = 0;
	pop64(val);
	*[register] (localidx:2 * 8) = val;
}
:""^indent^"local.tee" localidx is opc=0x22; localidx; indent {
	val:8 = 0;
	pop64(val);
	*[register] (localidx:2 * 8) = val;
	push64(val);
}
:""^indent^"global.get" globalidx is opc=0x23; globalidx; indent {
	val:8 = get_global(globalidx);
	push64(val);
}
:""^indent^"global.set" globalidx is opc=0x24; globalidx; indent {
	val:8 = 0;
	pop64(val);
	set_global(globalidx, val);
}

#TODO: table instructions
:""^indent^"i32.const" ULeb128 is opc=0x41; ULeb128; indent {
	val:4 = ULeb128:4;
	push32(val);
}

:""^indent^"i64.const" ULeb128 is opc=0x42; ULeb128; indent {
	val:8 = ULeb128:8;
	push64(val);
}

@include "BasicOps.sinc"

#TODO: 0x3f
